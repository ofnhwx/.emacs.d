#+TITLE: user-features.org
#+STARTUP: overview

* プロジェクト設定
** プロジェクトルートの取得
   #+begin_src emacs-lisp
   (setq e:project-root-mark '(".git/" ".hg/" ".svn/" ".git"))
   (defun e:project-root (dir)
     "指定した DIR のプロジェクトルートを返す."
     (let* ((dir (file-name-directory (expand-file-name dir)))
            (candidates (mapcar (lambda (name) (locate-dominating-file dir name)) e:project-root-mark))
            (root (car (sort candidates (lambda (this other) (> (length this) (length other)))))))
       (if root (abbreviate-file-name root) nil)))
   #+end_src
** プロジェクト名の取得
   #+begin_src emacs-lisp
   (defun e:project-name (root)
     "指定した ROOT のプロジェクトの名前をいい感じに返す."
     (let* ((root (directory-file-name root))
            (name (file-name-nondirectory root))
            (parent (e:project-root root)))
       (if parent
           (concat (e:project-name parent) "/" name)
         name)))
   #+end_src
* ヘッダーライン
** ファイル用
   #+begin_src emacs-lisp
   (defvar e:header-line-for-files-cache (make-hash-table :test 'equal))
   (defun e:header-line-for-files-format-internal (path &optional refname)
     (let* ((path    (or path ""))
            (refname (or refname ""))
            (key     (concat path refname)))
       (or (gethash key e:header-line-for-files-cache)
           (let* ((file  (f-filename path))
                  (dir   (f-short (file-name-directory path)))
                  (tramp (file-remote-p path))
                  (limit (- (window-width) (length file)))
                  (proot (e:project-root dir))
                  (pname (if proot (format "[%s]" (e:project-name proot)))))
             ;; プロジェクト内のファイルの場合はプロジェクト名を省略
             (when proot
               (setq dir   (replace-regexp-in-string (regexp-quote proot) "" dir))
               (setq limit (- limit (length pname))))
             ;; Tramp 接続の場合は該当箇所を省略
             (when tramp
               (setq dir   (replace-regexp-in-string (regexp-quote tramp) "" dir))
               (setq limit (- limit (length tramp))))
             ;; ディレクトリ部分を頑張って短くする
             (setq dir (e:shorten dir limit))
             (let ((index (s-index-of "..." dir)))
               (when index
                 (set-text-properties index (+ index 3) '(face font-lock-comment-face) dir)))
             ;; 属性を設定しつつ結合する
             (puthash key
                      (concat
                       (propertize " " 'display '(space :align-to 0))
                       (propertize (or tramp "") 'face 'font-lock-comment-face)
                       (propertize (or pname "") 'face 'font-lock-keyword-face)
                       dir
                       (propertize file 'face 'font-lock-constant-face)
                       (propertize refname 'face 'font-lock-keyword-face))
                      e:header-line-for-files-cache)))))
   (defun e:header-line-for-files-format ()
     "ファイル用のヘッダーラインのフォーマット"
     (e:header-line-for-files-format-internal
      (f-long (e:current-buffer-file-name))
      (e:current-buffer-refname)))
   (defun e:setup-headerline-for-files ()
     "ファイル用のヘッダーラインの設定"
     (set-face-attribute 'header-line nil
                         :inherit 'mode-line
                         :background (face-attribute 'mode-line :background))
     (setq-local spaceline-buffer-id-p nil)
     (setq-local header-line-format
                 '((:eval (e:header-line-for-files-format)))))
   #+end_src
* コマンドの結果を取得
** コマンドの結果を文字列で取得
   #+begin_src emacs-lisp
   (defun e:shell-command-to-string (command)
     "コマンドの結果を文字列で取得."
     (string-trim (shell-command-to-string command)))
   #+end_src
** コマンドの結果をリストで取得
   #+begin_src emacs-lisp
   (defun e:shell-command-to-list (command)
     "コマンドの結果をリストで取得."
     (split-string (e:shell-command-to-string command) "\n"))
   #+end_src
* その他の便利な関数
** 認証情報の取得
   #+begin_src emacs-lisp
   (defun e:auth-source-get (property &rest spec)
     "認証情報から SPEC に一致する項目の PROPERTY を取得する."
     (let ((plist (car (apply 'auth-source-search spec)))
           (pkey (intern (format ":%s" property))))
       (when plist
         (plist-get plist pkey))))
   #+end_src
** リストから指定された順番の要素を削除
   #+begin_src emacs-lisp
   (defun e:remove-nth (n list)
     "N 番目の要素を LIST から取り除いて返す."
     (if (or (zerop n) (null list))
         (cdr list)
       (cons (car list) (e:remove-nth (1- n) (cdr list)))))
   #+end_src
** パスの短縮
   #+begin_src emacs-lisp
   (defun e:shorten (path &optional max separator omit)
     "指定された FILENAME を MAX 以下の長さに短縮する.
     パスの区切り文字は SEPARATOR, 短縮時の省略表示を OMIT で指定する."
     (let* ((max (or max (window-width)))
            (separator (or separator "/"))
            (omit (or omit "..."))
            (parts (split-string (abbreviate-file-name path) separator))
            (length (length (abbreviate-file-name path)))
            (target (if (string-empty-p (car parts)) 3 2))
            (min-parts (+ target 3)))
       (when (and (> length max)
                  (> (length parts) min-parts))
         (setq length (+ length (length omit) (- (length (nth target parts)))))
         (setf (nth target parts) omit)
         (setq target (+ target 1))
         (while (and (> length max)
                     (> (length parts) min-parts))
           (setq length (- length (length (nth target parts)) 1))
           (setq parts (e:remove-nth target parts))))
       (string-join parts separator)))
   #+end_src
** メジャーモードの取得
   #+begin_src emacs-lisp
   (defun e:major-mode (buffer)
     (with-current-buffer buffer
       major-mode))
   #+end_src
** いい感じにファイル名を取得
   #+begin_src emacs-lisp
   (defun e:current-buffer-file-name ()
     (or buffer-file-name
         (bound-and-true-p magit-buffer-file-name)))
   #+end_src
** いい感じに Git の参照名を取得
   #+begin_src emacs-lisp
   (defun e:current-buffer-refname ()
     (if (bound-and-true-p magit-buffer-refname)
         (format "<%s>" magit-buffer-refname)))
   #+end_src
