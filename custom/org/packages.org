#+TITLE: config/packages.org
#+STARTUP: overview

* spacemacs
** 日本語環境
*** 言語環境
    #+begin_src emacs-lisp
    (set-language-environment "Japanese")
    #+end_src
*** エンコーディング
    #+begin_src emacs-lisp
    (let ((coding-system 'utf-8))
      (prefer-coding-system          coding-system)
      (set-default-coding-systems    coding-system)
      (set-buffer-file-coding-system coding-system)
      (set-terminal-coding-system    coding-system)
      (set-keyboard-coding-system    coding-system))
    #+end_src
*** ロケール
    #+begin_src emacs-lisp
    (progn
      (setenv "LANG" "ja_JP.UTF-8")
      (setenv "LC_ALL" "ja_JP.UTF-8"))
    #+end_src
** キーバインドの追加
   #+begin_src emacs-lisp
   (spacemacs/set-leader-keys
     "%" 'query-replace
     "&" 'async-shell-command
     "^" 'ace-window)
   (bind-keys
    :map global-map
    ("C-;" . spacemacs/default-pop-shell)
    :map ctl-x-map
    ("C-c" . helm-M-x))
   #+end_src
** 別名を追加
   #+begin_src emacs-lisp
   (defalias 'exit 'save-buffers-kill-terminal)
   (defalias 'yes-or-no-p 'y-or-n-p)
   #+end_src
** ヘッダーライン
*** バッファーの種類毎に振分け
    #+begin_src emacs-lisp
    (defun e:setup-header-line ()
      "ヘッダーラインをなるべくいい感じに設定する"
      (cl-loop for buffer in (--filter (not (buffer-local-value 'header-line-format it))
                                       (-map #'window-buffer (window-list)))
               do (with-current-buffer buffer
                    (cond
                     ((e:current-buffer-file-name)
                      (e:setup-header-line-for-files))))))
    #+end_src
*** 一定時間毎に実行
    #+begin_src emacs-lisp
    (run-with-timer 1.0 1.0 'e:setup-header-line)
    #+end_src
** モードライン
*** 現在のファイルの文字/改行コードを分かりやすく表示
    #+begin_src emacs-lisp
    (spaceline-define-segment buffer-encoding-abbrev
      "The line ending convention used in the buffer."
      (let ((buf-coding (format "%s" buffer-file-coding-system)))
        (list (replace-regexp-in-string "-with-signature\\|-unix\\|-dos\\|-mac" "" buf-coding)
              (concat (and (string-match "with-signature" buf-coding) "ⓑ")
                      (and (string-match "unix"           buf-coding) "ⓤ")
                      (and (string-match "dos"            buf-coding) "ⓓ")
                      (and (string-match "mac"            buf-coding) "ⓜ")
                      )))
      :separator " ")
    #+end_src
*** 不要なモードを非表示
    #+begin_src emacs-lisp
    (progn
      (spacemacs|diminish company-mode)
      (spacemacs|diminish company-box-mode)
      (spacemacs|diminish emoji-cheat-sheet-plus-display-mode)
      (spacemacs|diminish evil-owl-mode)
      (spacemacs|diminish helm-migemo-mode)
      (spacemacs|diminish hybrid-mode)
      (spacemacs|diminish smartparens-mode)
      (spacemacs|diminish which-key-mode)
      (spacemacs|diminish yas-minor-mode))
    #+end_src
** 雑多なやつ
*** shell の設定
    #+begin_src emacs-lisp
    (set-variable 'shell-file-name
                  (or (executable-find "zsh")
                      (executable-find "bash")
                      (executable-find "sh")))
    #+end_src
*** パスワード関連
    #+begin_src emacs-lisp
    (progn
      (set-variable 'epa-pinentry-mode 'loopback)
      (set-variable 'password-cache-expiry 3600)
      (set-variable 'plstore-encoded t))
    #+end_src
*** 折り返さない
    #+begin_src emacs-lisp
    (progn
      (setq-default truncate-lines t)
      (set-variable 'truncate-partial-width-windows nil))
    #+end_src
*** 最終行の改行は EditorConfig で管理する
    #+begin_src emacs-lisp
    (progn
      (set-variable 'mode-require-final-newline nil)
      (set-variable 'require-final-newline nil))
    #+end_src
*** ロックファイルを作成しない
    #+begin_src emacs-lisp
    (set-variable 'create-lockfiles nil)
    #+end_src
*** 右から左に読む言語に対応しない
    #+begin_src emacs-lisp
    (setq-default bidi-display-reordering nil)
    #+end_src
*** 特定のバッファを消させない
    #+begin_src emacs-lisp
    (dolist (buffer '("*scratch*" "*Messages*"))
      (with-current-buffer buffer
        (emacs-lock-mode 'kill)))
    #+end_src
** 個別設定
*** Mac
**** タイトルバー
     #+begin_src emacs-lisp
     (let ((items '((ns-transparent-titlebar . t)
                    (ns-appearance . dark))))
       (dolist (item items)
         (assq-delete-all (car item) initial-frame-alist)
         (assq-delete-all (car item) default-frame-alist)
         (add-to-list 'initial-frame-alist item)
         (add-to-list 'default-frame-alist item)))
     #+end_src
**** キーボード入力(option, command関連)
     #+begin_src emacs-lisp
     (when (spacemacs/system-is-mac)
       (set-variable 'ns-command-modifier 'meta)
       (set-variable 'ns-right-command-modifier 'super)
       (set-variable 'ns-alternate-modifier 'none))
     #+end_src
*** WSL
**** Windows側のブラウザを起動
     #+begin_src emacs-lisp
     (let ((cmd-exe "/mnt/c/Windows/System32/cmd.exe")
           (cmd-args '("/c" "start")))
       (when (file-exists-p cmd-exe)
         (set-variable 'browse-url-generic-program  cmd-exe)
         (set-variable 'browse-url-generic-args     cmd-args)
         (set-variable 'browse-url-browser-function 'browse-url-generic)))
     #+end_src
*** ローカル
    #+begin_src emacs-lisp
    (let ((private-config (expand-file-name "config.el" e:private-directory)))
      (when (file-exists-p private-config)
        (load-file private-config)))
    #+end_src
* ace-window
  #+begin_src emacs-lisp
  (use-package ace-window
    :bind
    (:map global-map
          ("C-^" . ace-window))
    :init
    (set-variable 'aw-keys (number-sequence ?1 ?9)))
  #+end_src
* atomic-chrome
  #+begin_src emacs-lisp
  (use-package atomic-chrome
    :config
    (atomic-chrome-start-server))
  #+end_src
* avy
  #+begin_src emacs-lisp
  (use-package avy
    :defer t
    :init
    (set-variable 'avy-keys (number-sequence ?a ?z))
    (set-variable 'avy-all-windows nil)
    (set-variable 'avy-all-windows-alt t))
  #+end_src
* company
  #+begin_src emacs-lisp
  (use-package company
    :no-require
    :bind
    (:map company-active-map
          ("<escape>" . company-abort)
          ("C-z" . company-try-hard))
    :init
    (define-prefix-command 'e:complete-command)
    (evil-define-key 'hybrid global-map (kbd "<backtab>") #'e:complete-command)
    (bind-keys
     :map global-map
     ("C-z" . company-try-hard)
     :map e:complete-command
     ("<backtab>" . ("Company" . company-complete))
     ("d" . ("Dabbrev" . company-dabbrev))
     ("f" . ("Files" . company-files))
     ("t" . ("TabNine" . company-tabnine))))
  #+end_src
* company-tabnine
  #+begin_src emacs-lisp
  (use-package company-tabnine
    :commands (company-tabnine)
    :init
    (set-variable 'company-tabnine-binaries-folder (expand-file-name "tabnine" e:private-directory))
    (spacemacs|add-company-backends
      :backends company-tabnine
      :modes haml-mode js2-mode php-mode ruby-mode))
  #+end_src
* codic
  #+begin_src emacs-lisp
  (use-package codic
    :defer t
    :config
    (set-variable 'codic-api-token (e:auth-source-get 'token :host "codic")))
  #+end_src
* dired
  #+begin_src emacs-lisp
  (use-package dired
    :defer t
    :init
    (set-variable 'dired-dwim-target t)
    (set-variable 'dired-listing-switches "-Ahl")
    (set-variable 'dired-omit-files "^\\.?#\\|^\\.$\\|^\\.\\.$\\|^\\.DS_Store")
    (set-variable 'dired-recursive-copies 'always)
    (set-variable 'dired-recursive-deletes 'always)
    :config
    (bind-keys
     :map dired-mode-map
     ("C-c C-e" . wdired-change-to-wdired-mode)))
  #+end_src
* dired > ls-lisp
  #+begin_src emacs-lisp
  (use-package ls-lisp
    :after (dired)
    :init
    (set-variable 'ls-lisp-dirs-first t)
    (set-variable 'ls-lisp-format-time-list '("%Y-%m-%d %H:%M:%S" "%Y-%m-%d %H:%M:%S"))
    (set-variable 'ls-lisp-ignore-case nil)
    (set-variable 'ls-lisp-use-insert-directory-program nil)
    (set-variable 'ls-lisp-use-localized-time-format t)
    (set-variable 'ls-lisp-verbosity '(uid gid)))
  #+end_src
* dired > ls-lisp-extension
  #+begin_src emacs-lisp
  (use-package ls-lisp-extension
    :after (ls-lisp))
  #+end_src
* dired-filter
  #+begin_src emacs-lisp
  (use-package dired-filter
    :after (dired)
    :config
    (add-hook 'dired-mode-hook 'dired-filter-mode))
  #+end_src
* display-line-numbers
  #+begin_src emacs-lisp
  (use-package display-line-numbers
    :hook ((find-file . e:display-line-numbers-mode-on)
           (prog-mode . e:display-line-numbers-mode-on)
           (html-mode . e:display-line-numbers-mode-on))
    :init
    (setq-default display-line-numbers-width 4)
    :config
    (defun e:display-line-numbers-mode-on ()
      "`display-line-numbers-mode'を有効化."
      (interactive)
      (display-line-numbers-mode 1))
    (defun e:display-line-numbers-mode-off ()
      "`display-line-numbers-mode'を無効化."
      (interactive)
      (display-line-numbers-mode 0)))
  #+end_src
* eaw
  #+begin_src emacs-lisp
  (use-package eaw
    :config
    (eaw-fullwidth))
  #+end_src
* ediff
  #+begin_src emacs-lisp
  (use-package ediff
    :commands (e:ediff)
    :config
    (defun e:ediff ()
      (interactive)
      (let ((files (->> (window-list (selected-frame))
                        (-map #'window-buffer)
                        (-map #'buffer-file-name)
                        (-non-nil))))
        (if (= (length files) 2)
            (ediff (nth 0 files)
                   (nth 1 files))
          (call-interactively #'ediff)))))
  #+end_src
* elfeed
  #+begin_src emacs-lisp
  (use-package elfeed
    :defer t
    :init
    (defvar e:elfeed-spacemacs-layout-name "@Elfeed")
    (defvar e:elfeed-spacemacs-layout-binding "f")
    (spacemacs|define-custom-layout e:elfeed-spacemacs-layout-name
      :binding e:elfeed-spacemacs-layout-binding
      :body
      (call-interactively 'elfeed)
      (define-advice elfeed-search-quit-window (:after (&rest _) kill-layout)
        (persp-kill e:elfeed-spacemacs-layout-name))))
  #+end_src
* elfeed-goodies
  #+begin_src emacs-lisp
  (use-package elfeed-goodies
    :defer t
    :init
    (set-variable 'elfeed-goodies/feed-source-column-width 30)
    (set-variable 'elfeed-goodies/tag-column-width 30))
  #+end_src
* elisp-demos
  #+begin_src emacs-lisp
  (use-package elisp-demos
    :defer t
    :init
    (advice-add 'describe-function-1 :after 'elisp-demos-advice-describe-function-1)
    (advice-add 'helpful-update :after 'elisp-demos-advice-helpful-update))
  #+end_src
* emmet-mode
  #+begin_src emacs-lisp
  (use-package emmet-mode
    :bind
    (:map emmet-mode-keymap
          ("<C-return>" . nil)
          ("C-c C-j" . emmet-expand-line)
          ("C-j" . nil)))
  #+end_src
* eshell
  #+begin_src emacs-lisp
  (use-package eshell
    :defer t
    :init
    (set-variable 'eshell-history-size 100000))
  #+end_src
* evil
  #+begin_src emacs-lisp
  (use-package evil
    :defer t
    :init
    (set-variable 'evil-cross-lines t)
    (set-variable 'evil-move-cursor-back nil)
    :config
    ;; キーバインド
    (setq evil-disable-insert-state-bindings t)
    (bind-keys
     ;; モーションモード(motion -> normal -> visual)
     :map evil-motion-state-map
     ("C-^" . nil) ;; evil-buffer
     ;; 通常モード
     :map evil-normal-state-map
     ("<down>" . evil-next-visual-line)
     ("<up>" . evil-previous-visual-line)
     ("j" . evil-next-visual-line)
     ("k" . evil-previous-visual-line)
     ("gj" . evil-avy-goto-line-below)
     ("gk" . evil-avy-goto-line-above)
     ("S" . evil-avy-goto-char-timer)
     ;; ビジュアルモード
     :map evil-visual-state-map
     ;; 挿入モード
     :map evil-insert-state-map
     ;; オペレーターモード
     :map evil-operator-state-map
     ;; 置き換えモード
     :map evil-replace-state-map
     ;; Emacsモード
     :map evil-emacs-state-map))
  #+end_src
* evil-easymotion
  #+begin_src emacs-lisp
  (use-package evil-easymotion
    :defer t
    :after (evil)
    :init
    (define-prefix-command 'e:evil-em-command)
    (bind-keys
     :map e:evil-em-command
     ("w"  . ("em/forward-word-begin" . evilem-motion-forward-word-begin))
     ("W"  . ("em/forward-WORD-begin" . evilem-motion-forward-WORD-begin))
     ("e"  . ("em/forward-word-end" . evilem-motion-forward-word-end))
     ("E"  . ("em/forward-WORD-end" . evilem-motion-forward-WORD-end))
     ("b"  . ("em/backward-word-begin" . evilem-motion-backward-word-begin))
     ("B"  . ("em/backward-WORD-begin" . evilem-motion-backward-WORD-begin))
     ("j"  . ("em/next-visual-line" . evilem-motion-next-visual-line))
     ("J"  . ("em/next-line" . evilem-motion-next-line))
     ("k"  . ("em/previous-visual-line" . evilem-motion-previous-visual-line))
     ("K"  . ("em/previous-line" . evilem-motion-previous-line))
     ("g"  . ("em/backward-word/WORD-end"))
     ("ge" . ("em/backward-word-end" . evilem-motion-backward-word-end))
     ("gE" . ("em/backward-WORD-end" . evilem-motion-backward-WORD-end))
     ("t"  . ("em/find-char-to" . evilem-motion-find-char-to))
     ("T"  . ("em/find-char-to-backward" . evilem-motion-find-char-to-backward))
     ("f"  . ("em/find-char" . evilem-motion-find-char))
     ("F"  . ("em/find-char-backward" . evilem-motion-find-char-backward))
     ("["  . ("em/backward-section"))
     ("[[" . ("em/backward-section-begin" . evilem-motion-backward-section-begin))
     ("[]" . ("em/backward-section-end" . evilem-motion-backward-section-end))
     ("]"  . ("em/forward-section"))
     ("]]" . ("em/forward-section-begin" . evilem-motion-forward-section-begin))
     ("][" . ("em/forward-section-end" . evilem-motion-forward-section-end))
     ("("  . ("em/backward-section-begin" . evilem-motion-backward-sentence-begin))
     (")"  . ("em/forward-section-begin" . evilem-motion-forward-sentence-begin))
     ("n"  . ("em/search-next" . evilem-motion-search-next))
     ("N"  . ("em/search-previous" . evilem-motion-search-previous))
     ("*"  . ("em/search-word-forward" . evilem-motion-search-word-forward))
     ("#"  . ("em/search-word-backward" . evilem-motion-search-word-backward))
     ("-"  . ("em/previous-line-first-non-blank" . evilem-motion-previous-line-first-non-blank))
     ("+"  . ("em/next-line-first-non-blank" . evilem-motion-next-line-first-non-blank))
     ("s"  . evil-avy-goto-char-timer))
    (bind-key "s" 'e:evil-em-command evil-normal-state-map)
    (bind-key "x" 'e:evil-em-command evil-visual-state-map)
    (bind-key "x" 'e:evil-em-command evil-operator-state-map))
  #+end_src
* evil-little-word
  #+begin_src emacs-lisp
  (use-package evil-little-word
    :after (evil)
    :catch
    (lambda (keyword error)
      (spacemacs-buffer/message "evil-little-word: %s" (error-message-string error))))
  #+end_src
* evil-textobj-between
  #+begin_src emacs-lisp
  (use-package evil-textobj-between
    :after (evil))
  #+end_src
* evil-owl
  #+begin_src emacs-lisp
  (use-package evil-owl
    :config
    (evil-owl-mode 1))
  #+end_src
* eww
  #+begin_src emacs-lisp
  (use-package eww
    :defer t
    :init
    (defvar e:eww-spacemacs-layout-name "@Eww")
    (defvar e:eww-spacemacs-layout-binding "w")
    (spacemacs|define-custom-layout e:eww-spacemacs-layout-name
      :binding e:eww-spacemacs-layout-binding
      :body
      (eww "https://www.google.com/")
      (define-advice quit-window (:after (&rest _) kill-layout)
        (persp-kill e:eww-spacemacs-layout-name))))
  #+end_src
* flycheck
  #+begin_src emacs-lisp
  (use-package flycheck
    :defer t
    :init
    (set-variable 'flycheck-idle-buffer-switch-delay 3.0)
    (set-variable 'flycheck-idle-change-delay 3.0))
  #+end_src
* git-gutter
  #+begin_src emacs-lisp
  (use-package git-gutter
    :defer t
    :config
    (dolist (face '(git-gutter:added
                    git-gutter:deleted
                    git-gutter:modified))
      (set-face-attribute face nil :background (face-attribute face :foreground))))
  #+end_src
* git-gutter+
  #+begin_src emacs-lisp
  (use-package git-gutter+
    :defer t
    :config
    (dolist (face '(git-gutter+-added
                    git-gutter+-deleted
                    git-gutter+-modified))
      (set-face-attribute face nil :background (face-attribute face :foreground))))
  #+end_src
* google-translate
  #+begin_src emacs-lisp
  (use-package google-translate
    :defer t
    :init
    (set-variable 'google-translate-default-source-language nil)
    (set-variable 'google-translate-default-target-language "ja"))
  #+end_src
* helm
  #+begin_src emacs-lisp
  (use-package helm
    :commands (e:helm-git-log)
    :bind
    (:map global-map
          ([remap eval-expression] . helm-eval-expression))
    :init
    (set-variable 'helm-buffer-max-length nil)
    (spacemacs/set-leader-keys
      "igl" 'e:helm-git-log)
    :config
    (progn
      (defvar e:helm-git-log-source
        (helm-build-in-buffer-source "Git log"
          :data #'e:helm-git-log-source-data
          :real-to-display #'e:helm-git-log-source-real-to-display
          :action #'e:helm-git-log-source-action))
      (defun e:helm-git-log-regexp ()
        "\\(.+\\)\x0000\\(.+\\)")
      (defun e:helm-git-log-source-data ()
        (s-split "\n" (shell-command-to-string "git log --pretty=format:'%H%x00%s' --no-merges")))
      (defun e:helm-git-log-source-real-to-display (candidate)
        (let ((regexp (e:helm-git-log-regexp)))
          (when (string-match regexp candidate)
            (format "%s %s"
                    (propertize (match-string 1 candidate) 'face 'font-lock-comment-face)
                    (match-string 2 candidate)))))
      (defun e:helm-git-log-source-action (candidate)
        (let ((regexp (e:helm-git-log-regexp)))
          (when (string-match regexp candidate)
            (insert (match-string 2 candidate) "\n"))))
      (defun e:helm-git-log ()
        (interactive)
        (helm :sources e:helm-git-log-source
              :buffer "*HELM Git log*")))
    (helm-migemo-mode))
  #+end_src
** after: eldoc-eval
   #+begin_src emacs-lisp
   (use-package helm
     :after (eldoc-eval)
     :bind
     (:map eldoc-in-minibuffer-mode-map
           ([remap eldoc-eval-expression] . helm-eval-expression)))
   #+end_src
* helm-fzf
  #+begin_src emacs-lisp
  (use-package helm-fzf
    :defer t
    :init
    (set-variable 'helm-fzf-args nil)
    (spacemacs/set-leader-keys
      "fz" 'helm-fzf
      "pz" 'helm-fzf-project-root))
  #+end_src
* helpful
  #+begin_src emacs-lisp
  (use-package helpful
    :defer t
    :init
    (spacemacs/declare-prefix "hdd" "helpful")
    (spacemacs/set-leader-keys
      "hddc" 'helpful-callable
      "hddd" 'helpful-at-point
      "hddf" 'helpful-function
      "hddi" 'helpful-command
      "hddk" 'helpful-key
      "hddm" 'helpful-macro
      "hdds" 'helpful-symbol
      "hddv" 'helpful-variable))
  #+end_src
* lsp-mode
  #+begin_src emacs-lisp
  (use-package lsp-mode
    :defer t
    :init
    (set-variable 'lsp-session-file (expand-file-name ".lsp-session-v1" spacemacs-cache-directory)))
  #+end_src
* lsp-java
  #+begin_src emacs-lisp
  (use-package lsp-java
    :defer t
    :init
    (set-variable 'lsp-java-server-install-dir (expand-file-name "java/lsp" spacemacs-cache-directory))
    (set-variable 'lsp-java-workspace-dir (expand-file-name "java/workspace" spacemacs-cache-directory)))
  #+end_src
* magit
  #+begin_src emacs-lisp
  (use-package magit
    :defer t
    :init
    (set-variable 'magit-log-margin '(t "%Y-%m-%d %H:%M" magit-log-margin-width t 15))
    (set-variable 'magit-diff-refine-hunk 'all)
    (set-variable 'magit-diff-refine-ignore-whitespace t)
    (set-variable 'smerge-refine-ignore-whitespace nil)
    :config
    (magit-add-section-hook 'magit-status-sections-hook #'magit-insert-skip-worktree-files nil t)
    (when (executable-find "ghq")
      (set-variable 'magit-repository-directories
                    (list (cons (e:shell-command-to-string "ghq root") 3))))
    (evil-define-key 'normal magit-mode-map (kbd "<escape>") 'ignore))
  #+end_src
* markdown-mode
  #+begin_src emacs-lisp
  (use-package markdown-mode
    :init
    (set-variable 'markdown-command "pandoc"))
  #+end_src
* notmuch
  #+begin_src emacs-lisp
  (use-package notmuch
    :defer t
    :init
    (set-variable 'notmuch-archive-tags '("-inbox" "-unread"))
    (set-variable 'notmuch-message-deleted-tags '("+trash" "-inbox"))
    (set-variable 'notmuch-column-control 1.0)
    (set-variable 'notmuch-hello-thousands-separator ",")
    (set-variable 'notmuch-search-oldest-first nil)
    (set-variable 'notmuch-show-empty-saved-searches nil)
    (set-variable 'notmuch-show-logo nil)
    (set-variable 'notmuch-hello-hide-tags
                  '(;; -------------------------
                    "drafts"    ;; +下書き
                    "flagged"   ;; +スター付き
                    "important" ;; +重要
                    "inbox"     ;; +受信トレイ
                    "sent"      ;; +送信済み
                    "spam"      ;; +迷惑メール
                    "trash"     ;; +ごみ箱
                    "unread"    ;; +未読
                    ;; -------------------------
                    "encrypted" ;; -暗号
                    "new"       ;; -新規(notmuch)
                    "signed"    ;; -署名
                    ;; -------------------------
                    ))
    (set-variable 'notmuch-saved-searches
                  '((:name "すべて"     :query "*"             :key "a")
                    (:name "受信トレイ" :query "tag:inbox"     :key "i")
                    (:name "未読"       :query "tag:unread"    :key "u")
                    (:name "スター付き" :query "tag:flagged"   :key "s")
                    (:name "重要"       :query "tag:important" :key "m")
                    (:name "送信済み"   :query "tag:sent"      :key "t")
                    (:name "下書き"     :query "tag:draft"     :key "d")
                    (:name "ごみ箱"     :query "tag:trash")
                    (:name "迷惑メール" :query "tag:spam")))
    (setenv "XAPIAN_CJK_NGRAM" "1"))
  #+end_src
* message
  #+begin_src emacs-lisp
  (use-package "message"
    :if (executable-find "msmtp")
    :init
    (set-variable 'sendmail-program (executable-find "msmtp"))
    (set-variable 'message-send-mail-function 'message-send-mail-with-sendmail)
    (set-variable 'message-sendmail-extra-arguments '("--read-envelope-from"))
    (set-variable 'message-sendmail-f-is-evil t)
    (set-variable 'message-kill-buffer-on-exit t))
  #+end_src
* open-by-jetbrains-ide
  #+begin_src emacs-lisp
  (use-package open-by-jetbrains-ide
    :load-path "custom/lisp"
    :init
    (when (spacemacs/system-is-mac)
     (set-variable 'jetbrains/use-toolbox-mode t)
     (set-variable 'jetbrains/ide-pstorm "phpstorm")
     (set-variable 'jetbrains/ide-mine   "rubymine"))
    :config
    (spacemacs/declare-prefix "aj" "jetbrains")
    (spacemacs/set-leader-keys
      "ajA" '("AppCode" . jetbrains/open-by-appcode)
      "ajC" '("CLion" . jetbrains/open-by-clion)
      "ajR" '("Rider" . jetbrains/open-by-rider)
      "ajc" '("PyCharm" . jetbrains/open-by-charm)
      "ajg" '("GoLand" . jetbrains/open-by-goland)
      "aji" '("IntelliJ IDEA" . jetbrains/open-by-idea)
      "ajj" '("Default" . jetbrains/open-by-ide)
      "ajm" '("RubyMine" . jetbrains/open-by-mine)
      "ajp" '("PhpStorm" . jetbrains/open-by-pstorm)
      "ajs" '("Android Studio" . jetbrains/open-by-studio)
      "ajw" '("WebStorm" . jetbrains/open-by-wstorm)
      ))
  #+end_src
* open-junk-file
  #+begin_src emacs-lisp
  (use-package open-junk-file
    :defer t
    :init
    (set-variable 'open-junk-file-format (expand-file-name "junk/%Y/%Y%m%d-%H%M%S." e:private-directory)))
  #+end_src
* org
  #+begin_src emacs-lisp
  (use-package org
    :no-require
    :after (org)
    :init
    (set-variable 'org-directory (expand-file-name "org/" e:private-directory))
    (set-variable 'org-default-notes-file (expand-file-name "notes.org" org-directory))
    (set-variable 'org-agenda-files (-union (list org-default-notes-file)
                                            (directory-files-recursively org-directory org-agenda-file-regexp)))
    (set-variable 'org-refile-targets '((org-agenda-files :maxlevel . 3)))
    (set-variable 'org-todo-keywords
                  '((sequence "TODO(t)" "STARTED(s)" "|" "DONE(d)")
                    (sequence "WAITING(w@)" "HOLD(h@)" "|" "CANCELLED(c@)")))
    (set-variable 'org-edit-src-content-indentation 0))
  #+end_src
* ob-restclient
  #+begin_src emacs-lisp
  (use-package ob-restclient
    :defer t
    :after (org)
    :init
    (unless (--find (eq (car it) 'restclient) org-babel-load-languages)
      (org-babel-do-load-languages 'org-babel-load-languages
                                   (append org-babel-load-languages '((restclient . t))))))
  #+end_src
* paradox-github
  #+begin_src emacs-lisp
  (use-package paradox-github
    :no-require
    :after (paradox-github)
    :init
    (set-variable 'paradox-github-token (e:auth-source-get 'token :host "paradox")))
  #+end_src
* persistent-scratch
  #+begin_src emacs-lisp
  (use-package persistent-scratch
    :init
    (set-variable 'persistent-scratch-save-file (expand-file-name "scratch" e:private-directory))
    :config
    (persistent-scratch-setup-default))
  #+end_src
* persp-mode
  #+begin_src emacs-lisp
  (use-package persp-mode
    :defer t
    :init
    (set-variable 'persp-kill-foreign-buffer-behaviour nil))
  #+end_src
* php-mode
  #+begin_src emacs-lisp
  (use-package php-mode
    :defer t
    :init
    (spacemacs|add-company-backends :modes php-mode))
  #+end_src
* prodigy
  #+begin_src emacs-lisp
  (use-package prodigy
    :commands (e:prodigy-start-service)
    :config
    (defun e:prodigy-start-service (name)
      (let ((service (prodigy-find-service name)))
        (when service
          (prodigy-start-service service)))))
  #+end_src
* recentf
  #+begin_src emacs-lisp
  (use-package recentf
    :defer t
    :init
    (set-variable 'recentf-max-menu-items 20)
    (set-variable 'recentf-max-saved-items 3000)
    (set-variable 'recentf-filename-handlers '(abbreviate-file-name)))
  #+end_src
* rubocopfmt
  #+begin_src emacs-lisp
  (use-package rubocopfmt
    :defer t
    :init
    (set-variable 'rubocopfmt-use-bundler-when-possible nil))
  #+end_src
* ruby-mode
  #+begin_src emacs-lisp
  (use-package ruby-mode
    :defer t
    :init
    (set-variable 'ruby-insert-encoding-magic-comment nil))
  #+end_src
* shr
  #+begin_src emacs-lisp
  (use-package shr
    :defer t
    :init
    (set-variable 'shr-use-colors nil)
    (set-variable 'shr-max-image-proportion 0.6))
  #+end_src
* skk
  #+begin_src emacs-lisp
  (use-package skk
    :hook ((evil-hybrid-state-entry . e:skk-mode)
           (evil-hybrid-state-exit  . skk-mode-exit))
    :bind
    (:map global-map
          ([remap toggle-input-method] . skk-mode)
          ("C-¥" . skk-mode))
    :init
    (set-variable 'default-input-method "japanese-skk")
    (progn
      (set-variable 'skk-user-directory (expand-file-name "ddskk" e:private-directory))
      (set-variable 'skk-large-jisyo (expand-file-name "dic-mirror/SKK-JISYO.L" e:external-directory)))
    (progn
      (set-variable 'skk-preload t)
      (set-variable 'skk-egg-like-newline t)
      (set-variable 'skk-share-private-jisyo t)
      (set-variable 'skk-show-annotation t)
      (set-variable 'skk-show-inline 'vertical)
      (set-variable 'skk-sticky-key ";")
      (set-variable 'skk-use-jisx0201-input-method t))
    (when (executable-find "google-ime-skk")
      (set-variable 'skk-server-prog (executable-find "google-ime-skk"))
      (set-variable 'skk-server-inhibit-startup-server t)
      (set-variable 'skk-server-host "127.0.0.1")
      (set-variable 'skk-server-portnum 55100))
    :config
    ;; skk の有効化で半角英数入力にする
    (defun e:skk-mode ()
      (interactive)
      (if (bound-and-true-p skk-mode)
          (skk-latin-mode-on)
        (let ((skk-mode-hook (-union skk-mode-hook '(skk-latin-mode-on))))
          (skk-mode))))
    ;; skk-study を有効化
    (require 'skk-study nil t)
    ;; google-ime-skk を起動
    (when (and (executable-find "google-ime-skk")
               (require 'prodigy nil t))
      (prodigy-define-service
        :name "google-ime-skk"
        :command "google-ime-skk"
        :tags '(general)
        :kill-signal 'sigkill)
      (defun e:prodigy:google-ime-skk ()
        (interactive)
        (e:prodigy-start-service "google-ime-skk"))
      (e:prodigy:google-ime-skk)))
  #+end_src
* so-long
  #+begin_src emacs-lisp
  (use-package so-long
    :config
    (global-so-long-mode 1))
  #+end_src
* tramp
  #+begin_src emacs-lisp
  (use-package tramp
    :defer t
    :init
    (set-variable 'tramp-default-host "localhost"))
  #+end_src
* tramp-sh
  #+begin_src emacs-lisp
  (use-package tramp-sh
    :after (tramp)
    :config
    ;; ssh/conf.d の内容を接続先に追加
    (let ((functions (->> (ignore-errors (f-files "~/.ssh/conf.d/hostsa" nil t))
                          (--map (list #'tramp-parse-sconfig it)))))
      (--each '("ssh" "scp")
        (let ((new-functions (-union (tramp-get-completion-function it) functions)))
          (tramp-set-completion-function it new-functions)))))
  #+end_src
* transient
  #+begin_src emacs-lisp
  (use-package transient
    :no-require
    :init
    (set-variable 'transient-default-level 7))
  #+end_src
* treemacs
  #+begin_src emacs-lisp
  (use-package treemacs
    :defer t
    :init
    (set-variable 'treemacs-persist-file (expand-file-name "treemacs-persist" spacemacs-cache-directory))
    (set-variable 'treemacs-last-error-persist-file (expand-file-name "treemacs-persist-at-last-error" spacemacs-cache-directory)))
  #+end_src
* url-cache
  #+begin_src emacs-lisp
  (use-package url-cache
    :defer t
    :init
    (set-variable 'url-cache-directory (expand-file-name "url/cache" spacemacs-cache-directory)))
  #+end_src
* url-cookie
  #+begin_src emacs-lisp
  (use-package url-cookie
    :defer t
    :init
    (set-variable 'url-cookie-file (expand-file-name "url/cookies" spacemacs-cache-directory)))
  #+end_src
* visual-regexp
  #+begin_src emacs-lisp
  (use-package visual-regexp
    :bind
    (:map global-map
          ([remap query-replace] . vr/query-replace)))
  #+end_src
* whitespace
  #+begin_src emacs-lisp
  (use-package whitespace
    :hook ((find-file . e:whitespace-mode-on)
           (prog-mode . e:whitespace-mode-on))
    :init
    (set-variable 'whitespace-style
                  '(face
                    trailing
                    tabs
                    tab-mark
                    spaces
                    space-mark
                    newline
                    newline-mark))
    (set-variable 'whitespace-space-regexp "\\(\u3000+\\)")
    (set-variable 'whitespace-display-mappings
                  '((space-mark   ?\u3000 [?\u30ed])
                    (tab-mark     ?\t     [?\t])
                    (newline-mark ?\n     [?\u0024 ?\n])))
    :config
    (set-face-attribute 'whitespace-trailing nil :background "#800000")
    (let ((color "#595D63"))
      (set-face-attribute 'whitespace-tab      nil :foreground color :strike-through t)
      (set-face-attribute 'whitespace-space    nil :foreground color)
      (set-face-attribute 'whitespace-newline  nil :foreground color))
    (defun e:whitespace-mode-on ()
      (interactive)
      (whitespace-mode 1)))
  #+end_src
